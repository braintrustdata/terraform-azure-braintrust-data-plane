# tflint-ignore-file: terraform_module_pinned_source

module "braintrust" {
  source = "github.com/braintrustdata/terraform-azure-braintrust-data-plane"
  # Append '?ref=<version_tag>' to lock to a specific version of the module.

  ### Examples below are set to production-sized defaults. Feel free to change them to your needs.

  # This is primarily used for tagging and naming resources in your Azure account.
  # Do not change this after deployment. Most resources can not be renamed without destroying them.
  deployment_name = "braintrust"

  # Azure region to deploy resources to
  location = "eastus2"

  # Add your organization name from the Braintrust UI here
  braintrust_org_name = ""

  # You can get this License Key from the Braintrust UI in Settings > Data Plane > Brainstore License Key.
  brainstore_license_key = var.brainstore_license_key

  ### Postgres configuration
  # Postgres Compute: https://learn.microsoft.com/en-us/azure/postgresql/flexible-server/concepts-compute
  # Postgres Storage: https://learn.microsoft.com/en-us/azure/postgresql/flexible-server/concepts-storage
  postgres_sku_name     = "MO_Standard_E16ds_v4" # 16 core, 128GB memory, 25600 Max IOPS, 384MiB/s Max Throughput
  postgres_storage_mb   = 8388608                # 8TB
  postgres_storage_tier = "P70"                  # 18000 IOPS, 750MB/s Max Throughput


  ### Redis configuration
  # Redis: https://azure.microsoft.com/en-us/pricing/details/cache/
  redis_sku_name = "Standard"
  redis_family   = "C"
  redis_capacity = 2 #2.5GB memory
  redis_version  = "6"


  ### AKS configuration
  create_aks_cluster            = true
  aks_brainstore_pool_vm_size   = "Standard_D32ds_v6" # 32 core, 128GB memory, 1700Gi storage
  aks_brainstore_pool_max_count = 10
  aks_services_pool_vm_size     = "Standard_D16s_v6" # 16 core, 64GB memory, no storage
  aks_services_pool_max_count   = 10


  ### Ingress
  # This can only be enabled after the AKS cluster is created and the Helm chart is deployed. See the README for more details.
  enable_front_door = false
  # IP address of the internal load balancer generated by AKS for the API service
  front_door_api_backend_address = ""
  # Resource ID of the internal load balancer frontend IP configuration
  front_door_load_balancer_frontend_ip_config_id = ""


  ### Network configuration
  # WARNING: You should choose these values carefully after discussing with your networking team.
  # Changing them after the fact is not possible and will require a complete rebuild of your Braintrust deployment.

  # CIDR block for the VNet. The core Braintrust services will be deployed in this VNet.
  vnet_address_space_cidr = "10.185.0.0/20"
}
